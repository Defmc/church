use crate::parser::Token;
use crate::{UBody, UTerm, Ast};
grammar;

pub Program: Ast = {
    <e:ProgramAtom> "\n" => Ast::Program(vec![e]),
    <mut p:Program> <e:ProgramAtom> "\n" => {
        if let Ast::Program(ref mut v) = p {
            v.push(e);
        }
        p
    }
}

pub ProgramAtom: Ast = {
    Assign => <>
}

pub Assign: Ast = <i:Ident> "=" <e:Expr> => Ast::Assign(i, e);

pub Expr: UTerm = {
    AtomExpr => <>,
    <e:Expr> <atom:AtomExpr> => UBody::App(e, atom).into()
}

AtomExpr: UTerm = {
    LambdaAbs => <>,
    Ident => UBody::Var(<>).into(),
    "(" <e:Expr> ")" => e,
}

LambdaAbs: UTerm = LambdaSym <i:Ident> DotSym "(" <e:Expr> ")" => UBody::Abs(i, e).into();

extern {
    type Location = usize;
    type Error = ();
    
    enum Token {
        LambdaSym => Token::Lambda,
        DotSym => Token::Dot,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,
        "=" => Token::Assign,
        "\t" => Token::Tab,
        "\n" => Token::NewLine,
        Ident => Token::Ident(<String>)
    }
}
